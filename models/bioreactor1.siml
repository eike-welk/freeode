MODEL BioReactor
    PARAMETER #Define  values that stay constant during the simulation.
        mu_max; Ks; Yxs; Sf;

    VARIABLE #Define values that change during the simulation.
        mu; X; S;

    EQUATION #Specify the equations
        mu := mu_max * S/(S+Ks);        #growth speed (of biomass)
        $X := mu*X;                     #change of biomass concentration
        $S := -1/Yxs*mu*X;              #change of sugar concentration
END


#Biological reactor with no inflow or outfow
PROCESS Batch #Define a concrete simulation expriment
    PARAMETER #Define the constant values again.
        mu_max; Ks; Yxs; Sf;

    UNIT #Say which model will be used.
        r AS BioReactor;

    SET #Set values to the parameters. The values will be copied into the BioReactor model
        mu_max := 0.32; #max growth speed
        Ks     := 0.01; #at this sugar concentration growth speed is 0.5 * mu_max
        Yxs    := 0.5;  #one g sugar gives this much biomass
        Sf     := 20;   #sugar concentration in feed liquid (inflow)

    INITIAL #Set initial values to the state variables. Note: the full path has to be specified for variables.
        r.X := 0.1; #initial biomass concentration
        r.S := 20;  #initial sugar concentration

    SOLUTIONPARAMETERS #Specify options for the simulation algorithm.
        ReportingInterval :=0.5; #[h] time between data points
        SimulationTime    :=20;  #[h] total simulation time
END
